plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'
    id 'maven'
    id 'application'
    id 'edu.sc.seis.launch4j' version '2.4.6'
    // id 'edu.sc.seis.macAppBundle' version '2.3.0'
    id "nebula.ospackage" version "8.2.0"
    id 'org.gradle.crypto.checksum' version '1.2.0'
    id 'com.palantir.git-version' version '0.12.2'
    id 'io.spring.dependency-management' version '1.0.1.RELEASE' //for logging - log4j
	
	  id 'nu.studer.jooq' version '3.0.3'
	  
	  id "org.flywaydb.flyway" version "8.2.0"
	
    id "de.undercouch.download" version "1.2"
    
    id 'jacoco'
}


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'com.github.signum-network:signumj:v0.20.0'
    compile 'io.reactivex:rxjava:1.3.8'
    compile 'org.bouncycastle:bcprov-jdk15on:1.69'
    compile 'org.ehcache:ehcache:3.8.0'
    compile 'org.flywaydb:flyway-core:8.2.0'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.h2database:h2:1.4.199'
    compile 'commons-cli:commons-cli:1.4'
    compile 'com.zaxxer:HikariCP:3.3.1'
    compile 'org.jocl:jocl:2.0.1'
    compile 'org.jooq:jooq:3.14.8'
    compile 'org.mariadb.jdbc:mariadb-java-client:2.4.3'
    compile 'org.owasp.encoder:encoder:1.2.2'
    compile 'org.bitlet:weupnp:0.1.4'
    
    compile 'com.google.zxing:core:3.4.0'
    compile 'com.google.zxing:javase:3.4.0'
    
    compile 'org.slf4j:slf4j-api:1.7.28'
    compile 'org.slf4j:slf4j-jdk14:1.7.28'
    
    compile 'org.eclipse.jetty:jetty-server:9.4.43.v20210629'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.43.v20210629'
    compile 'org.eclipse.jetty:jetty-servlets:9.4.43.v20210629'
    compile 'org.eclipse.jetty:jetty-rewrite:9.4.43.v20210629'
    
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    
    compile 'org.apache.httpcomponents:httpclient:4.5.13'

    compile 'com.github.jiconfont:jiconfont:1.0.0'
    compile 'com.github.jiconfont:jiconfont-swing:1.0.1'
    compile 'com.github.jiconfont:jiconfont-font_awesome:4.7.0.1'

    //logging
    compile 'org.apache.logging.log4j:log4j-api:2.16.0'
    compile 'org.apache.logging.log4j:log4j-core:2.16.0'

    // Use JUnit test framework
    testImplementation("org.junit.platform:junit-platform-engine:1.5.1")
    testImplementation("org.junit.platform:junit-platform-launcher:1.5.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.1")
    
    testImplementation 'org.mockito:mockito-core:3.0.0'
    
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    
    // Needed for build script so "compile" needed
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.4.1'
    
    //jooqRuntime "com.h2database:h2:1.4.199"
    jooqRuntime group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.4.1'
}
test {
    useJUnitPlatform()
}

sourceSets {
  main {
        java {
            srcDir "src"
        }
        resources {
            srcDir "resources"
        }
  }
  test {
      java {
        srcDirs = ["test/java"]
      }
  }
}

// Define the main class for the application
mainClassName = 'brs.BurstLauncher'

description = 'Signum Node.'

def schema_dburl =  "jdbc:mariadb://localhost:3306/signum_jooq"
def schema_dbusername = "root"
def schema_dbpassword = "pass"
flyway {
    url = "${schema_dburl}"
    user = "${schema_dbusername}"
    password = "${schema_dbpassword}"
    locations = ["filesystem:"+project.projectDir.toString()+"/resources/db/migration_mariadb"]
}

jooq {
    version = "3.14.8"
    edition = "OSS"
    signum(sourceSets.main) {
        jdbc {
            url = "${schema_dburl}"
            user = "${schema_dbusername}"
            password = "${schema_dbpassword}"
        }
        generator {
            name = 'org.jooq.codegen.JavaGenerator'
            database {
                includes = ".*"
                
                name = "org.jooq.meta.mariadb.MariaDBDatabase"
                inputSchema = "signum_jooq"
                outputSchema = "DB"
                
                excludes = ''
                // outputSchemaToDefault = true
                
                forcedTypes {
                    forcedType {
                        name = 'BOOLEAN'
                        expression = '.*\\.(.*\\.LATEST|MINTABLE|AT_STATE\\.FREEZE_WHEN_SAME_BALANCE|GOODS\\.DELISTED|TRANSACTION\\.(HAS_MESSAGE|HAS_ENCRYPTED_MESSAGE|HAS_PUBLIC_KEY_ANNOUNCEMENT|HAS_ENCRYPTTOSELF_MESSAGE)|PURCHASE\\.(PENDING|HAS_FEEDBACK_NOTES|HAS_PUBLIC_FEEDBACKS))'
                        types = '.*'
                    }
                }
            }
            target {
                packageName = "brs.schema"
                directory = "src"
            }
        }
    }
}

// use the following to generate the schema tables:
// ./gradlew build -DgenerateSchema=true --rerun-tasks
generateSignumJooqSchemaSource.dependsOn flywayMigrate
generateSignumJooqSchemaSource.onlyIf {System.getProperty("generateSchema") == "true"}
flywayMigrate.onlyIf {System.getProperty("generateSchema") == "true"}


def details = versionDetails()

jar {
	// Needed by the loggin library
    manifest {
        attributes 'Multi-Release': 'true'
    }
}

shadowJar{
    archiveName = "${baseName}-${classifier}.${extension}"
}


createExe {
    outfile = 'signum-node.exe'
    icon = "${projectDir}/resources/images/signum_overlay_logo.ico"
    jreMinVersion = '1.8.0'
    copyright = 'signum.network'
    companyName = 'https://signum.network'
    textVersion = details.lastTag
    
    //bundledJrePath = "./jre"
    //bundledJre64Bit = false
    dontWrapJar = true

    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "signum-node.jar"
}

task dist(type: Zip) {
   dependsOn createExe
   from (project.tasks.shadowJar.outputs.files){
    include 'signum-node-all.jar'
    rename {'signum-node.jar'}
   }
   from "${project.buildDir}/launch4j/${baseName}.exe"
   into('conf') {from "conf/brs-default.properties"} 
   into('conf') {from "conf/logging-default.properties"} 
   into('html') {from "html"}
   from "LICENSE.txt"
   from "README.md"
   archiveName "${baseName}-${details.lastTag}.zip"
}

import org.gradle.crypto.checksum.Checksum

task release(type: Checksum, dependsOn: [shadowJar, dist
	//createInstaller
	]) {
    files = project.tasks.shadowJar.outputs.files
    //.plus(files(['build/distributions/signum-node-win_x64.zip', ]))
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
